apply plugin: "jaci.openrio.gradle.GradleRIO"

deploy {
    targets {
        target("roborio", jaci.openrio.gradle.frc.RoboRIO) {
            team = 5333
        }
    }
    artifacts {
        // Define my robot artifact. The FRCJavaArtifact type gives access to
        // robotCommand and other tools necessary for the RoboRIO to detect
        // and run our code.
        artifact('robotCode', jaci.openrio.gradle.frc.FRCJavaArtifact) {
            targets << "roborio"
        }
    }
}

// Note - the robot code doesn't need these repositories.
// However, the 'common' project does, and for some reason it won't pull
// its dependencies unless the repository is listed here.  Keep that in mind.
repositories {
  mavenCentral() // Used for the GSON library (Google's JSON converter)
}

// Defining my dependencies. In this case, WPILib (+ friends), CTRE Toolsuite (Talon SRX)
// and NavX.
dependencies {
    // Here is an example of how to include common code between multiple subsystems using Gradle.
    // It's useful for  common constants (like ports #'s) and common utility code.
    compile project(':common')

    compile wpilib()
    compile ctre()
    compile navx()
}

jar {
    from configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    manifest jaci.openrio.gradle.GradleRIOPlugin.javaManifest('gradlerio.RobotExample')
}